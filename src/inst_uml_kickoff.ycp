/**
 * Module:	inst_uml_kickoff.ycp
 *
 * Authors:	Arvin Schnell <arvin@suse.de>
 *
 * Purpose:	Do various tasks before starting with uml installation.
 *
 * $Id$
 */
{
    import "UML";
    import "Directory";
    import "Progress";
    import "Report";
    import "Package";


    textdomain "uml";


    path umlpath = .uml.options;


    /**
     *
     */
    define boolean install_packages () ``{

	if (!Package::InstallAll (["um-host-kernel",
				   "um-host-install-initrd",
				   "uml-utilities"]))
	{
	    Report::Error (_("Cannot install required packages."));
	    return false;
	}

	return true;
    }


    /**
     *
     */
    define boolean create_user () ``{

	if (!UML::CreateUser())
	{
	    Report::Error (sformat(_("Cannot create UML user %1"), UML::username));
	    return false;
	}

	return true;
    }


    /**
     *
     */
    define void register_options_file () ``{
        string scr_file = (string)(SCR::Read (.target.tmpdir)) + "/uml_option.scr";
        SCR::Write (.target.string, scr_file,
		    sformat ("%1\n\n`ag_ini(`SysConfigFile(\"%2\"))\n", umlpath,
			     sformat ("%1/OPTIONS", UML::homedir)));
        SCR::RegisterAgent (umlpath, scr_file);
    }


    /**
     *
     */
    define void unregister_options_file () ``{
	SCR::UnregisterAgent (umlpath);
    }


    /**
     *
     */
    define void save_options () ``{

	SCR::Write (add (umlpath, "MEMORY"), UML::memory_size);
	SCR::Write (add (umlpath, "NCPUS"), UML::number_of_cpus);

	SCR::Write (add (umlpath, "USERNAME"), UML::username);

	SCR::Write (add (umlpath, "TAP_DEVICE"), UML::tap_device);
	SCR::Write (add (umlpath, "MAC_ADDRESS"), UML::MAC_address);

	SCR::Write (add (umlpath, "EXTRA_ARGS"), "");

	// flush
	SCR::Write (umlpath, nil);
	// change owner
	SCR::Execute(.target.bash, sformat("/bin/chown %1:users %2/OPTIONS", UML::username, UML::homedir));
	// change mode
	SCR::Execute(.target.bash, sformat("/bin/chmod 0600 %1/OPTIONS", UML::homedir));
    }


    /**
     *
     */
    define boolean make_disks () ``{

	SCR::Write (add (umlpath, "NDISKS"), size (UML::disks));

	integer i = 0;
	foreach (integer s, UML::disks, ``{

	    string disk_file = sformat ("ubd%1", i);

	    SCR::Write (add (umlpath, sformat ("DISK%1", i)), disk_file);

	    string cmd = "/bin/dd if=/dev/zero of=" + UML::homedir + "/" +
		disk_file + " bs=1M " + sformat ("count=%1", s);
	    SCR::Execute (.target.bash, cmd);
	    // change owner
	    SCR::Execute(.target.bash, sformat("/bin/chown %1:users %2/%3", UML::username, UML::homedir, disk_file));
	    // change mode
	    SCR::Execute(.target.bash, sformat("/bin/chmod 0600 %1/%2", UML::homedir, disk_file));

	    i = i + 1;

	});

	return true;
    }


    /**
     *
     */
    define boolean copy_files () ``{

	list <string> files_kernel = Pkg::PkgGetFilelist ("um-host-kernel",
							  `installed);
	list <string> files_initrd = Pkg::PkgGetFilelist ("um-host-install-initrd",
							  `installed);

	files_kernel = filter (string s, files_kernel,
			       ``(substring (s, 0, size ("/usr/bin/linux")) ==
				  "/usr/bin/linux"));

	files_initrd = filter (string s, files_initrd,
			       ``(substring (s, 0, size ("/usr/lib/initrd")) ==
				  "/usr/lib/initrd"));

	string kernel = files_kernel[0]:"";
	string initrd = files_initrd[0]:"";

	y2milestone ("kernel and initrd %1 %2", kernel, initrd);

	if (kernel == "" || initrd == "")
	{
	    return false;
	}

	SCR::Execute (.target.bash, "/bin/cp " + kernel + " " + UML::homedir +
		      "/linux");
	// change owner
	SCR::Execute(.target.bash, sformat("/bin/chown %1:users %2/linux", UML::username, UML::homedir));
	// change mode
	SCR::Execute(.target.bash, sformat("/bin/chmod 0700 %1/linux", UML::homedir));
	SCR::Write (add (umlpath, "KERNEL"), "linux");

	SCR::Execute (.target.bash, "/bin/cp " + initrd + " " + UML::homedir +
		      "/initrd");
	// change owner
	SCR::Execute(.target.bash, sformat("/bin/chown %1:users %2/initrd", UML::username, UML::homedir));
	// change mode
	SCR::Execute(.target.bash, sformat("/bin/chmod 0600 %1/initrd", UML::homedir));
	SCR::Write (add (umlpath, "INITRD"), "initrd");

	return true;
    };


    /**
     *
     */
    define void start_uml () ``{

	map general_data = Pkg::SourceGeneralData (UML::source_id);
	string url = general_data["url"]:"";

	SCR::Execute (.target.bash, "cd " + UML::homedir + " ; " +
		      Directory::ybindir + "/start-uml " +
		      " install=" + url);

    }


    list <string> progress_stages = [
				     // progress stages
				     _("Install packages"),
				     // progress stages
				     _("Create user"),
				     // progress stages
				     _("Save options"),
				     // progress stages
				     _("Create virtual disks"),
				     // progress stages
				     _("Copy kernel and initrd")
    ];

    list progress_descriptions = [];

    integer progress_length = 16;

    // Headline for UML Installation
    string headline = _("Preparing UML Installation");

    // FIXME
    string help_text = "FIXME.";


    // Headline for UML Installation
    Progress::New (headline, "", progress_length,
		   progress_stages, progress_descriptions, help_text);

    install_packages ();

    Progress::NextStage ();

    create_user ();

    Progress::NextStage ();

    register_options_file ();

    save_options ();

    Progress::NextStage ();

    make_disks ();

    Progress::NextStage ();

    copy_files ();

    unregister_options_file ();

    Progress::Finish ();


    start_uml ();


    return `next;
}
